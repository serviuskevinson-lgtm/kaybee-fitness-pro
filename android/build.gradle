// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.2'
        classpath 'com.google.gms:google-services:4.4.2'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:2.1.0'
        classpath 'org.jetbrains.kotlin:compose-compiler-gradle-plugin:2.1.0'
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    // Force specific versions to avoid requirements for compileSdk 36 (API 36)
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'androidx.activity') {
                details.useVersion '1.9.3'
            }
        }
    }
}

// Workaround for 'proguard-android.txt' no longer supported in AGP 8+
subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android.buildTypes.all { buildType ->
                def files = buildType.proguardFiles
                if (files.any { it.name == 'proguard-android.txt' }) {
                    def updatedFiles = files.collect { 
                        it.name == 'proguard-android.txt' ? project.android.getDefaultProguardFile('proguard-android-optimize.txt') : it 
                    }
                    buildType.setProguardFiles(updatedFiles)
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.layout.buildDirectory
}
